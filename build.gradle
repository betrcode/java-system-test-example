plugins {
    id 'java'
    id 'maven' // For mavenDeployer
}

group = 'se.bettercode.systemtest'

repositories {
    jcenter()
}

dependencies {
    testCompile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'org.slf4j:slf4j-simple:1.7.25'
    testCompile 'io.rest-assured:rest-assured:3.0.7'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'javax.ws.rs:javax.ws.rs-api:2.1'
    testCompile 'javax.xml.bind:jaxb-api:2.3.0'  // Needed to work on Java 9
    testCompile 'com.sun.xml.bind:jaxb-core:2.3.0'  // Needed to work on Java 9
    testCompile 'com.sun.xml.bind:jaxb-impl:2.3.0'  // Needed to work on Java 9
    testCompile 'org.yaml:snakeyaml:1.20'
    testCompileOnly 'org.projectlombok:lombok:1.16.20'
}

test {
    systemProperties System.properties
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        showStandardStreams = false
        exceptionFormat = 'full'
    }
    exclude 'se/bettercode/systemtest/**Test.class' // Because all tests are included in a suite, and the suites will be run.
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.testCompile.collect { it.getName() }.join(' ')
        )
    }
    from(
            [
                    sourceSets.test.output, // Will include the tests in the jar
                    configurations.testCompile.collect { it.isDirectory() ? it : zipTree(it) }  // Will make a fat jar including testCompile dependencies
            ]
    )
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://somehost/content/repositories/some-releases") {
                authentication(userName: 'testuser', password: 'testpass')
            }
        }
    }
}
